//Multi-controller for Wii Remote
//Version 1.1
//Made by Glix

//Say “hello world”

If Wiimote.Home = 1 Then
	If Wiimote.HasNunchuk = 1 Then
		Var.Up = Wiimote.Nunchuk.Up
		Var.Right = Wiimote.Nunchuk.Right
		Var.Left = Wiimote.Nunchuk.Left
		Var.Down = Wiimote.Nunchuk.Down

		If Wiimote.Nunchuk.C = 1 Then
			Var.C = 8
		Else
			Var.C = 0
		Endif

		If Var.Up AND !Var.Right AND !Var.Left AND !Var.Down Then
			Var.D = 1
		Elseif Var.Up AND Var.Right AND !Var.Left AND !Var.Down Then
			Var.D = 2
		Elseif (!Var.Up) AND Var.Right AND !Var.Left AND !Var.Down Then
			Var.D = 3
		Elseif (!Var.Up) AND Var.Right AND !Var.Left AND Var.Down Then
			Var.D = 4
		Elseif (!Var.Up) AND !Var.Right AND !Var.Left AND Var.Down Then
			Var.D = 5
		Elseif (!Var.Up) AND !Var.Right AND Var.Left AND Var.Down Then
			Var.D = 6
		Elseif (!Var.Up) AND !Var.Right AND Var.Left AND !Var.Down Then
			Var.D = 7
		Elseif Var.Up AND !Var.Right AND Var.Left AND !Var.Down Then
			Var.D = 8
		Else
			Var.D = 1
		Endif

		Var.Controller = Var.C + Var.D
	Else
		Var.Up = Wiimote.Up
		Var.Right = Wiimote.Right
		Var.Left = Wiimote.Left
		Var.Down = Wiimote.Down
		
		If Wiimote.B = 1 Then
			Var.B = 4
		Else
			Var.B = 0
		Endif

		If Var.Up AND !Var.Right AND !Var.Left AND !Var.Down Then
			Var.A = 1
		Elseif (!Var.Up) AND Var.Right AND !Var.Left AND !Var.Down Then
			Var.A = 2
		Elseif (!Var.Up) AND !Var.Right AND !Var.Left AND Var.Down Then
			Var.A = 3
		Elseif (!Var.Up) AND !Var.Right AND Var.Left AND !Var.Down Then
			Var.A = 4
		Else
			Var.A = 1
		Endif

		Var.Controller = Var.B + Var.A
	Endif
Endif

If Var.Controller = 1 Then
	If Wiimote.HasNunchuk = 1 Then
		Var.Dummy = 0
	Else
		//PhotoSlide
		Key.Up = Wiimote.Right
		Key.Down = Wiimote.Left
		Key.Right = Wiimote.Down
		Key.Left = Wiimote.Up

		If Wiimote.Minus = 1 Then
			Key.Escape = Wiimote.A
			Key.Shift+Key.Delete = Wiimote.One
			Key.Alt+Key.F4 = Wiimote.Two
		Else
			Key.Enter = Wiimote.A
			Key.Ctrl+Key.Comma = Wiimote.One
			Key.Ctrl+Key.Dot = Wiimote.Two
		Endif

		Key.Alt = Wiimote.Plus
		
		If Wiimote.Plus = 1 Then
			Key.Tab = Wiimote.B
		Else
			Key.BackSpace = Wiimote.B
		Endif
	Endif

	Wiimote.Led1 = 0
	Wiimote.Led2 = 0
	Wiimote.Led3 = 0
	Wiimote.Led4 = 1
	
Elsif Var.Controller = 2 Then
	If Wiimote.HasNunchuk = 1 Then
		Var.Dummy = 0
	Else
		//Bastion
		
		If (Wiimote.A OR Var.Move = 3 OR Var.Move = 2) AND Var.Move != 1 Then
			If Var.Move != 2 Then
				Var.Move = 3
			EndIf
			
			If (Var.Move = 3 AND Wiimote.Two) OR Var.Move = 2 Then
				
				Var.Move = 2
				
				If Var.Move = 2 AND Wiimote.B Then
					Var.Move = 1
				Elsif Var.Move = 2 AND (Wiimote.One OR Wiimote.Plus OR Wiimote.Minus OR Wiimote.A OR Wiimote.Shake) Then
					Var.Move = 0
				EndIf
				
			Elsif Var.Move = 3 AND (Wiimote.One OR Wiimote.Plus OR Wiimote.Minus OR Wiimote.B OR Wiimote.Shake) Then
				Var.Move = 0
			EndIf
		EndIf

		Key.F = Wiimote.Plus
		Key.Q = Wiimote.Minus
		Key.R = Wiimote.A + Wiimote.B
		Key.E = Wiimote.A
		Key.Shift = Wiimote.B

		Key.Space = Wiimote.Shake
		
		If Var.Move = 1 then
			If Wiimote.Up then
				Mouse.CursorPos = [Mouse.CursorPosX - 10, Mouse.CursorPosY]
			Endif

			If Wiimote.Down then
				Mouse.CursorPos = [Mouse.CursorPosX + 10, Mouse.CursorPosY]
			Endif

			If Wiimote.Left then
				Mouse.CursorPos = [Mouse.CursorPosX, Mouse.CursorPosY + 10]
			Endif

			If Wiimote.Right then
				Mouse.CursorPos = [Mouse.CursorPosX, Mouse.CursorPosY - 10]
			Endif

			Key.Escape = Wiimote.A
			Key.Tab = Wiimote.Two
			Mouse.LeftButton = Wiimote.One

			If Wiimote.Minus Then
				Var.Move = 0
			EndIf
		Else
		
			Var.X = screen2.Width / 2
			Var.Y = screen2.Height / 2

			If Wiimote.Up then
				Key.A = Wiimote.Up
				Var.X = Var.X - screen2.Width / 4
			Else
				Key.A = 0
			Endif

			If Wiimote.Down then
				Key.D = Wiimote.Down
				Var.X = Var.X + screen2.Width / 4
			Else
				Key.D = 0
			Endif

			If Wiimote.Left then
				Key.S = Wiimote.Left
				Var.Y = Var.Y + screen2.Height / 4
			Else
				Key.S = 0
			Endif

			If Wiimote.Right then
				Key.W = Wiimote.Right
				Var.Y = Var.Y - screen2.Height / 4
			Else
				Key.W = 0
			Endif
			
			If Wiimote.Right OR Wiimote.Left OR Wiimote.Up OR Wiimote.Down Then
				Mouse.CursorPos = [Var.X, Var.Y]
			Endif

			Mouse.LeftButton = Wiimote.One
			Mouse.RightButton = Wiimote.Two
		Endif
	Endif

	Wiimote.Led1 = 0
	Wiimote.Led2 = 0
	Wiimote.Led3 = 1
	Wiimote.Led4 = 0
	
Elsif Var.Controller = 3 Then
	If Wiimote.HasNunchuk = 1 Then
		Var.Dummy = 0
	Else
		//Mouse
		Var.Fixed = 10

		If Var.Gain = 0 Then
		   Var.Gain = Var.Fixed
		Endif

		If Wiimote.Plus then
		   If Wiimote.Right then
			  Mouse.WheelUp = Wiimote.Right
		   Endif

		   If Wiimote.Left then
			  Mouse.WheelDown = Wiimote.Left
		   Endif

		   If Wiimote.One then
			  Mouse.MiddleButton = Wiimote.One
		   Endif
		   
		   If Wiimote.Two = 1 Then
			   Var.Gain = Var.Gain + 15

			   If Var.Gain > 45 Then
				  Var.Gain = Var.Fixed
			   Endif

			   wait 500 ms
			Endif
		Else
		   If Wiimote.Up then
			  Mouse.CursorPos = [Mouse.CursorPosX - var.Gain, Mouse.CursorPosY]
		   Endif

		   If Wiimote.Down then
			  Mouse.CursorPos = [Mouse.CursorPosX + var.Gain, Mouse.CursorPosY]
		   Endif

		   If Wiimote.Left then
			  Mouse.CursorPos = [Mouse.CursorPosX, Mouse.CursorPosY + var.Gain]
		   Endif

		   If Wiimote.Right then
			  Mouse.CursorPos = [Mouse.CursorPosX, Mouse.CursorPosY - var.Gain]
		   Endif
		Endif
		
		Key.Ctrl = Wiimote.Minus
		Key.Alt = Wiimote.B
		Key.Tab = Wiimote.A

		Mouse.LeftButton = Wiimote.One
		Mouse.RightButton = Wiimote.Two
	Endif
	
	Wiimote.Led1 = 0
	Wiimote.Led2 = 0
	Wiimote.Led3 = 1
	Wiimote.Led4 = 1
   
Elsif Var.Controller = 4 Then
	If Wiimote.HasNunchuk = 1 Then
		Var.Dummy = 0
	Else
		//Monaco
		
		Key.LeftControl = Wiimote.One
		Key.LeftShift = Wiimote.Two
		Key.Space = Wiimote.A
		Key.E = Wiimote.B
		Key.V = Wiimote.Plus
		Key.Escape = Wiimote.Minus
		
		Var.X = 683
		Var.Y = 384

		If Wiimote.Up then
			Key.A = Wiimote.Up
			Var.X = Var.X - 383
		Else
			Key.A = 0
		Endif

		If Wiimote.Down then
			Key.D = Wiimote.Down
			Var.X = Var.X + 383
		Else
			Key.D = 0
		Endif

		If Wiimote.Left then
			Key.S = Wiimote.Left
			Var.Y = Var.Y + 184
		Else
			Key.S = 0
		Endif

		If Wiimote.Right then
			Key.W = Wiimote.Right
			Var.Y = Var.Y - 184
		Else
			Key.W = 0
		Endif
		
		If Wiimote.Right OR Wiimote.Left OR Wiimote.Up OR Wiimote.Down Then
			Mouse.CursorPos = [Var.X, Var.Y]
		Endif
		
	Endif

	Wiimote.Led1 = 0
	Wiimote.Led2 = 1
	Wiimote.Led3 = 0
	Wiimote.Led4 = 0
	
Elsif Var.Controller = 5 Then
	If Wiimote.HasNunchuk = 1 Then
		Var.Dummy = 0
	Else
		//Bleed
		
		If Wiimote.A then
			wait 500 ms
			Toggle(Mouse.LeftButton)
		Endif
		
		Key.Q = Wiimote.Minus
		Key.Space = Wiimote.One
		Mouse.RightButton = Wiimote.Two
		
		Var.W = Window.Width
		Var.H = Window.Height
		
		//Var.X = Var.W * 0.5
		//Var.Y = Var.H * 0.5

		If Wiimote.Up then
			Key.A = Wiimote.Up
			Var.X = Var.W * 0.25
			Var.Y = Var.H * 0.5
		Else
			Key.A = 0
		Endif

		If Wiimote.Down then
			Key.D = Wiimote.Down
			Var.X = Var.W * 0.75
			Var.Y = Var.H * 0.5
		Else
			Key.D = 0
		Endif

		If Wiimote.Left then
			Key.S = Wiimote.Left
			Var.X = Var.W * 0.5
			Var.Y = Var.H * 0.75
		Else
			Key.S = 0
		Endif

		If Wiimote.Right then
			Key.W = Wiimote.Right
			Var.X = Var.W * 0.5
			Var.Y = Var.H * 0.25
		Else
			Key.W = 0
		Endif
		
		If Wiimote.Right OR Wiimote.Left OR Wiimote.Up OR Wiimote.Down Then
			Mouse.CursorPos = [Var.X, Var.Y]
		Endif
		
	Endif
	
	Wiimote.Led1 = 0
	Wiimote.Led2 = 1
	Wiimote.Led3 = 0
	Wiimote.Led4 = 1
	
Elsif Var.Controller = 6 Then
   Wiimote.Led1 = 0
   Wiimote.Led2 = 1
   Wiimote.Led3 = 1
   Wiimote.Led4 = 0
Elsif Var.Controller = 7 Then
   Wiimote.Led1 = 0
   Wiimote.Led2 = 1
   Wiimote.Led3 = 1
   Wiimote.Led4 = 1
Elsif Var.Controller = 8 Then
   Wiimote.Led1 = 1
   Wiimote.Led2 = 0
   Wiimote.Led3 = 0
   Wiimote.Led4 = 0
Elsif Var.Controller = 9 Then
   Wiimote.Led1 = 1
   Wiimote.Led2 = 0
   Wiimote.Led3 = 0
   Wiimote.Led4 = 1
Elsif Var.Controller = 10 Then
   Wiimote.Led1 = 1
   Wiimote.Led2 = 0
   Wiimote.Led3 = 1
   Wiimote.Led4 = 0
Elsif Var.Controller = 11 Then
   Wiimote.Led1 = 1
   Wiimote.Led2 = 0
   Wiimote.Led3 = 1
   Wiimote.Led4 = 1
Elsif Var.Controller = 12 Then
   Wiimote.Led1 = 1
   Wiimote.Led2 = 1
   Wiimote.Led3 = 0
   Wiimote.Led4 = 0
Elsif Var.Controller = 13 Then
   Wiimote.Led1 = 1
   Wiimote.Led2 = 1
   Wiimote.Led3 = 0
   Wiimote.Led4 = 1
Elsif Var.Controller = 14 Then
   Wiimote.Led1 = 1
   Wiimote.Led2 = 1
   Wiimote.Led3 = 1
   Wiimote.Led4 = 0
Elsif Var.Controller = 15 Then
   Wiimote.Led1 = 1
   Wiimote.Led2 = 1
   Wiimote.Led3 = 1
   Wiimote.Led4 = 1
Elsif Var.Controller = 16 Then
   Wiimote.Led1 = 0
   Wiimote.Led2 = 0
   Wiimote.Led3 = 0
   Wiimote.Led4 = 0
EndIf

